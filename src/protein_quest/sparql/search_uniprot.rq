#+ summary: Search for UniProt accessions based on various criteria
#+ description: Returns UniProt protein accessions filtered by organism, review status, subcellular location, molecular function, and sequence length
#+ endpoint: https://sparql.uniprot.org/sparql
#+ method: GET
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX GO:<http://purl.obolibrary.org/obo/GO_>

SELECT DISTINCT ?protein
WHERE {
    ?protein a up:Protein .

    # Taxon filter (optional)
    OPTIONAL {
        FILTER(BOUND(?_taxon_id_iri))
        ?protein up:organism ?_taxon_id_iri .
    }

    # Reviewed filter (optional)
    OPTIONAL {
        FILTER(BOUND(?_reviewed_value))
        ?protein up:reviewed ?_reviewed_value .
    }

    # Subcellular location UniProt (optional)
    OPTIONAL {
        FILTER(BOUND(?_subcell_label))
        ?protein up:annotation ?subcellAnnotation .
        ?subcellAnnotation up:locatedIn/up:cellularComponent ?cellcmpt .
        ?cellcmpt skos:prefLabel ?_subcell_label .
    }

    # Subcellular location GO (optional)
    OPTIONAL {
        FILTER(BOUND(?_subcell_go_iri))
        ?protein up:classifiedWith|(up:classifiedWith/rdfs:subClassOf+) ?_subcell_go_iri .
    }

    # Molecular function GO (optional)
    OPTIONAL {
        FILTER(BOUND(?_mol_func_go_iri))
        ?protein up:classifiedWith|(up:classifiedWith/rdfs:subClassOf+) ?_mol_func_go_iri .
    }

    # Sequence length filter (optional)
    OPTIONAL {
        FILTER(BOUND(?_min_seq_length) || BOUND(?_max_seq_length))
        ?protein up:sequence ?isoform .
        ?isoform a up:Simple_Sequence .
        BIND (IRI(STRBEFORE(REPLACE(
            STR(?isoform), "http://purl.uniprot.org/isoforms/", "http://purl.uniprot.org/uniprot/"
        ), "-")) AS ?ac_of_isoform)
        FILTER (?protein = ?ac_of_isoform)
        ?isoform rdf:value ?sequence .
        BIND (STRLEN(?sequence) AS ?seq_length)
        FILTER(
            (!BOUND(?_min_seq_length) || ?seq_length >= ?_min_seq_length) &&
            (!BOUND(?_max_seq_length) || ?seq_length <= ?_max_seq_length)
        )
    }
}
LIMIT 10000
